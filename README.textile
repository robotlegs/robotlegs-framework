h1. Robotlegs

Robotlegs is a pure AS3 micro-architecture for Flash and Flex application development.

Robotlegs helps you design loosely-coupled components that are easy to test, helps you wire those components together, and provides a simple mechanism for communicating between them.

h2. Installation

**Flex/FlashBuilder:**

Drop the following SWC file into your "libs" folder:
* RobotLegsLib.swc

If you are building a plain ActionScript project you might need to create the "libs" folder manually:

Right click the project, and create a New Folder called "libs".
Right click the project, open "properties", "Flex Build Path", "Library path", "Add SWC Folder...", and add the folder "libs".

**Other IDEs or Editors:**

Include the necessary SWC file in your build path.

h2. Terminology

Robotlegs uses the same terminology as PureMVC for many of it's components and concepts, such as:

Mediators, View Components, Proxies and Commands

Collectively, these are referred to as Framework Actors.

h2. Usage

**The Context**

Typically, when starting a new project, you extend the default mvcs Context and override the startup() method.

Inside the startup() method you bind a couple of Commands to a startup event and then dispatch that event.

<pre>
    public class HelloFlexContext extends Context
    {
    	public function HelloFlexContext( contextView:DisplayObjectContainer )
    	{
    		super( contextView );
    	}
    	
    	override public function startup():void
    	{
    		commandFactory.mapCommand( ContextEvent.STARTUP, PrepModelCommand, true );
    		commandFactory.mapCommand( ContextEvent.STARTUP, PrepControllerCommand, true );
    		commandFactory.mapCommand( ContextEvent.STARTUP, PrepServicesCommand, true );
    		commandFactory.mapCommand( ContextEvent.STARTUP, PrepViewCommand, true );
    		commandFactory.mapCommand( ContextEvent.STARTUP, StartupCommand, true );
    		eventBroadcaster.dispatchEvent( new ContextEvent( ContextEvent.STARTUP ) );
    	}
    }
</pre>

Instantiate the Context and pass it a reference to your view. For a Flex application it might look like this:

<pre>
    <mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" preinitialize="onPreInitialize()">
    	<mx:Script>
    		<![CDATA[
    			import net.boyblack.robotlegs.demos.helloflex.HelloFlexContext;
    			
    			private var context:HelloFlexContext;
    			
    			private function onPreInitialize():void
    			{
    				context = new HelloFlexContext( this );
    			}
    		]]>
    	</mx:Script>
    </mx:Application>
</pre>

If you are building a plain ActionScript application, your main Sprite (entry point) might look like this:

<pre>
    package
    {
    	import flash.display.Sprite;
    	import net.boyblack.robotlegs.demos.hello.HelloContext;
      
    	public class HelloActionScript extends Sprite
    	{
    		private var context:HelloContext;
		    
    		public function HelloActionScript()
    		{
    			context = new HelloContext( this );
    		}
    	}
    }
</pre>

By default, a Context will automatically execute it's startup() method when it's View Component is added to the Stage.

**Commands**

Robotlegs make use of native Flash Player events for framework communication. Much like PureMVC, Commands can be bound to events.

No parameters are passed to a Command's execute method however. Instead, you define the concrete event that will be passed to the Command as a dependency. This relieves you from having to cast the event.

Multiple Commands can be bound to an event type. They will be executed in the order that they were mapped. This is very handy for mapping your startup commands.

To get a reference to the concrete event that triggered a Command, you must declare the event as a Dependency:

<pre>
    public class TryClearMessages extends Command
    {
    	[Inject]
    	public var event:SystemEvent;
      
    	[Inject]
    	public var userProxy:UserProxy;
      
    	[Inject]
    	public var messageProxy:MessageProxy;
      
    	override public function execute():void
    	{
    		if ( userProxy.userLoggedIn )
    		{
    			messageProxy.clearMessages();
    		}
    		else
    		{
    			contextView.addChild( new LoginPage() );
    		}
    	}
    }
</pre>

**Mediators**

Robotlegs makes it easy to work with deeply-nested, lazily-instantiated View Components.

You map Mediator classes to View Component classes during startup, or later during runtime, and RobotLegs creates and registers Mediator instances automatically as View Components arrive on the stage (as children of the Context View).

A Mediator is only ready to be interacted with when it's onRegister method gets called. This is where you should register your listeners.

The default Mediator implementation provides a handy utility method called addEventListenerTo(). You should use this method to register listeners in your Mediator. Doing so allows RobotLegs to automatically remove any listeners when a Mediator gets removed.

A Mediator might look something like this:

<pre>
    public class HelloFormMediator extends Mediator
    {
    	[Inject]
    	public var helloForm:HelloForm;
      
    	[Inject]
    	public var messageProxy:MessageProxy;
      
    	override public function onRegister():void
    	{
    		// View Listeners
    		addEventListenerTo( helloForm, HelloFormEvent.FORM_SUBMITTED, onFormSubmitted );
    		// Context Listeners
    		addEventListenerTo( eventDispatcher, MessageProxyEvent.MESSAGE_ADDED, whenMessageAdded );
    	}
      
    	private function onFormSubmitted( e:HelloFormEvent ):void
    	{
    		messageProxy.addMessage( helloForm.getMessage() );
    	}
      
    	private function whenMessageAdded( e:MessageProxyEvent ):void
    	{
    		helloForm.messageTxt.setFocus();
    	}
    }
</pre>

The Mediator above has two dependencies:

* It's View Component: HelloForm
* The MessageProxy

It listens to events from the view, invokes methods on the MessageProxy's API, and listens to the Context's event bus for MessageProxyEvent events.

NOTE: addEventListenerTo() is a convenience method. It keeps track of any listeners registered and removes them when the Mediator is removed.

**Proxies**

Proxies are much like Mediators, but instead of wrapping View Components, they manage access to data (or Models).

Proxies should not listen to the Context's event bus, and therefore they are provided with one-way EventDispatchers called EventBroadcasters (a simple wrapper around the EventDispatcher that limits it's API).

**Services**

Services are like Proxies, but instead of managing Models, they manage access to remote services.

h2. Resources

h3. News

* "Follow Robotlegs on Twitter":http://twitter.com/robotlegs_as3/

h3. Overview

* [[Overview]]

h3. Code

* "Robotlegs Library":http://github.com/darscan/robotlegs
* "Robotlegs Demos":http://github.com/darscan/robotlegsdemos
* "Robotlegs Unit Tests":http://github.com/darscan/robotlegstests

h3. Help, Support, Collaboration

* "Robotlegs Discussion Group":http://groups.google.com/group/robotlegs
* "Robotlegs Issue Tracking":http://github.com/darscan/robotlegs/issues

h3. Tutorials and such on the Interblogs

* "Robotlegs Bloggoweb Postings - Shaun Smith":http://shaun.boyblack.co.za/blog/category/robotlegs/
* "Flex Image Gallery Demo - Joel Hooks":http://joelhooks.com/2009/07/17/robotlegs-as3-a-dependency-injection-driven-mvcs-framework-for-flashflex-%E2%80%93-inspired-by-puremvc/
* "FlexUnit 4 Unit Testing and Robotlegs - Joel Hooks":http://joelhooks.com/2009/07/21/unit-testing-with-inversion-of-control-ioc-and-dependency-injection-di-with-the-robotlegs-framework-and-flexunit-4/

h3. Other Resources

* "SwiftSuspenders":http://github.com/tschneidereit/SwiftSuspenders
* "SmartyPants-IOC":http://code.google.com/p/smartypants-ioc/
* "Spring ActionScript":http://www.springactionscript.org/
* "AS3 Commons":http://www.as3commons.org/

h2. License

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is furnished
to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.